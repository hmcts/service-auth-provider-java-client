plugins {
    id 'pmd'
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'org.springframework.boot' version '2.0.3.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'org.owasp.dependencycheck' version '3.2.1'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
}

group 'uk.gov.hmcts.reform'
version '1.0.3'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.11'
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "5.8.1"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("$projectDir/ruleset.xml")
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }
    jcenter()
    mavenLocal()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/service-auth-provider-java-client"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'service-auth-provider-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'idam service auth provider api')
                root.appendNode('name', 'Service auth provider api client')
                root.appendNode('url', 'https://github.com/hmcts/service-auth-provider-java-client')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'service-auth-provider-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/service-auth-provider-java-client'
        version {
            name = project.version
        }
    }
}

ext {
    reformLogging = '3.0.1'
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.0.0.RELEASE'
    compile group: 'com.netflix.feign', name: 'feign-jackson', version: '8.18.0'
    compile group: 'com.warrenstrange', name: 'googleauth', version: '1.1.5'
    compile group: 'com.auth0', name: 'java-jwt', version: '3.4.0'

    compile group: 'uk.gov.hmcts.reform', name: 'java-logging', version: reformLogging

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.10.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
}

dependencyManagement {
    dependencies {
        dependency group: 'com.google.guava', name: 'guava', version: '23.2-jre'
        dependency group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.60'
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

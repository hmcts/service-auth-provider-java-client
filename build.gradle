plugins {
    id 'pmd'
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.springframework.boot' version '3.2.2'
    id 'org.owasp.dependencycheck' version '9.0.9'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'se.patrikerdes.use-latest-versions' version '0.2.18'
    id 'java-library'
    id 'maven-publish'
    id 'au.com.dius.pact' version '4.1.11'
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "0.0.1-SNAPSHOT"

group 'com.github.hmcts'
version buildNumber

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '10.13.0'
    configDirectory = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.55.0"
    ignoreFailures = false
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

task contract(type: Test) {
    description = 'Runs the consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.rootDir', "pacts"
}

task runAndPublishConsumerPactTests(type: Test){
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

artifacts {
    archives sourcesJar
    archives javadocJar
}

def gitRepo = "https://github.com/hmcts/service-auth-provider-java-client"

publishing {
    publications {
        Main(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
            artifactId 'service-auth-provider-java-client'

            pom {
                name = 'Service auth provider API client'
                description = 'IDAM service auth provider API'
                url = gitRepo
                licenses {
                    license {
                        name = 'MIT License, Copyright (c) 2023 HM Courts & Tribunals Service'
                        url = "https://www.opensource.org/licenses/MIT"
                    }
                }
                scm {
                    url = gitRepo
                }
            }
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    implementation group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '4.1.0'
    implementation group: 'io.github.openfeign', name: 'feign-jackson', version: '13.2.1'
    implementation group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
    implementation group: 'com.auth0', name: 'java-jwt', version: '4.4.0'
    implementation group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.0.0'


    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.25.3'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.10.0'


    integrationTestImplementation sourceSets.main.runtimeClasspath
    integrationTestImplementation sourceSets.test.runtimeClasspath
    integrationTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    integrationTestImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '4.1.1'

    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.1.11'
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: '4.1.11'
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    contractTestImplementation("org.junit.jupiter:junit-jupiter-api:5.10.2")
    contractTestRuntimeOnly ("org.junit.jupiter:junit-jupiter-engine:5.10.2")
    contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.10.2')

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

tasks.withType(Copy).all {
    duplicatesStrategy DuplicatesStrategy.WARN
}

pact {
    publish {
        pactDirectory = 'pacts'
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
        tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
        version = project.pactVersion
    }
}

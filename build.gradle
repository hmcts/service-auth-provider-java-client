plugins {
    id 'pmd'
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.springframework.boot' version '2.4.1'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'org.owasp.dependencycheck' version '6.0.5'
    id 'com.github.ben-manes.versions' version '0.36.0'
    id 'java-library'
    id 'maven'
    id 'maven-publish'
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "0.0.1-SNAPSHOT"

group 'uk.gov.hmcts.reform'
version buildNumber

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.18'
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.12.0"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }
    jcenter()
    mavenLocal()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def gitRepo = "https://github.com/hmcts/service-auth-provider-java-client"

publishing {
    publications {
        Main(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Service auth provider API client'
                description = 'IDAM service auth provider API'
                url = gitRepo
                licenses {
                    license {
                        name = 'MIT License, Copyright (c) 2019 HM Courts & Tribunals Service'
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }
                scm {
                    url = gitRepo
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'service-auth-provider-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/service-auth-provider-java-client'
        version {
            name = project.version
        }
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '2.2.6.RELEASE'
    compile group: 'io.github.openfeign', name: 'feign-jackson', version: '10.12'
    compile group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
    compile group: 'com.auth0', name: 'java-jwt', version: '3.12.0'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '4.0.1'

    compileOnly group: 'uk.gov.hmcts.reform', name: 'java-logging', version: '5.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.13.1'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.18.1'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.10.19'

    integrationTestCompile sourceSets.main.runtimeClasspath
    integrationTestCompile sourceSets.test.runtimeClasspath
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    integrationTestCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: '2.2.5.RELEASE'
}

dependencyManagement {
    dependencies {
        dependency group: 'com.google.guava', name: 'guava', version: '30.1-jre'
    }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

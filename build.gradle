buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}
plugins {
    id 'checkstyle'
    id 'io.spring.dependency-management' version '1.0.3.RELEASE'
    id 'org.springframework.boot' version '1.5.9.RELEASE'
}

apply plugin: 'checkstyle'
apply plugin: 'java-library'
apply plugin: 'org.springframework.boot'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

group 'uk.gov.hmcts.reform'
version '0.0.2'

checkstyle {
    maxWarnings = 0
    toolVersion = '8.3'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
//    configDir = new File(rootDir, 'config/checkstyle')
}

sourceCompatibility = 1.8

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    maven {
        url "https://dl.bintray.com/hmcts/hmcts-maven"
    }
    jcenter()
    mavenLocal()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/service-authorisation-access-client"
    }
}


task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'service-authorisation-access-client'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with idam service authorisation access')
                root.appendNode('name', 'Service authorisation access client')
                root.appendNode('url', 'https://github.com/hmcts/service-authorisation-access-client')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'service-authorisation-access-client'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/service-authorisation-access-client'
        version {
            name = project.version
        }
    }
}

ext {
    springCloudVersion = 'Dalston.SR4'
}

dependencies {
    compile('org.springframework.cloud:spring-cloud-starter-feign')
    compile('com.netflix.feign:feign-jackson:8.18.0')
    compile group: 'com.google.guava', name: 'guava', version: '23.0'
    compile group: 'com.warrenstrange', name: 'googleauth', version: '1.1.2'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.8.0'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

bootRepackage {
    enabled = false;
}
